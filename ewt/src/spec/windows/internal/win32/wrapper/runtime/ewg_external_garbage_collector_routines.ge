indexing

	description:

		"External Garbage Collector routines"

	library: "Eiffel Wrapper Generator Library"
	copyright: "Copyright (c) 1999, Andreas Leitner and others"
	license: "Eiffel Forum License v2 (see forum.txt)"
	date: "$Date: 2003/12/29 16:54:52 $"
	revision: "$Revision: 1.1 $"


class EWG_EXTERNAL_GARBAGE_COLLECTOR_ROUTINES

feature
#ifdef XYZ
	--ISE || HACT
	eif_freeze (obj: ANY): POINTER is
			-- Make sure that `obj' will not be moved by GC.
		require
			obj_not_void: obj /= Void
		external
			"C [macro <eif_eiffel.h>]"
		alias
			"eif_freeze"
		end
 
	eif_unfreeze (ptr: POINTER) is
			-- Object at address `ptr' can be handled
			-- and moved by the GC now.
		require
			ptr_not_null: ptr /= default_pointer
		external
			"C [macro <eif_eiffel.h>] (EIF_REFERENCE)"
		alias
			"eif_unfreeze"
		end

	need_freezing: BOOLEAN is True

#endif
#ifdef XYZ
-- SE

	eif_freeze (obj: ANY): POINTER is
			-- Make sure that `obj' will not be moved by GC.
		require
			obj_not_void: obj /= Void
		do
			Result := obj.to_pointer
		end
 
	eif_unfreeze (ptr: POINTER) is
			-- Object at address `ptr' can be handled
			-- and moved by the GC now.
		require
			ptr_not_null: ptr /= default_pointer
		do
		end

	need_freezing: BOOLEAN is False

#endif
#ifdef VE
-- TODO
#endif


#ifdef ISE || HACT
	eif_adopt (a_object: ANY): POINTER is
		require
			a_object_not_void: a_object /= Void
		external
			"C [macro <eif_macros.h>]"
		alias
			"eif_adopt"
		ensure
			eif_adopt_not_default_pointer: Result /= Default_pointer
		end

	eif_wean (a_pointer: POINTER) is
		require
			a_pointer_not_void: a_pointer /= Default_pointer
		external
			"C [macro <eif_macros.h>]"
		alias
			"eif_wean"
		end

	eif_access (a_pointer: POINTER): ANY is
		require
			a_pointer_not_void: a_pointer /= Default_pointer
		external
			"C [macro <eif_macros.h>]"
		alias
			"eif_access"
		ensure
			eif_access_not_void: Result /= Void
		end

#endif
#ifdef SE
	adopted: FIXED_ARRAY[ANY] is
	once
		 create Result.with_capacity(16)
	end
	
	eif_adopt(a_object: ANY): POINTER is
		 -- A reference to object is stored in `adopted' to avoid
			-- it's collection at GC time.
	require
		a_object_not_void: a_object /= Void
	do
		 if not adopted.fast_has(a_object) then
			adopted.add_last(a_object)
		 end
		 Result := a_object.to_pointer
	ensure
		adopted_not_default_pointer: Result /= Default_pointer
	end
	
	eif_wean(a_pointer: POINTER) is
		 -- Remove the reference to `a_pointer' in adopted.
	require
		a_pointer_not_void: a_pointer /= Default_pointer
	local
		 i: INTEGER
	do
		 i := adopted.fast_index_of(eif_access (a_pointer))
		 if adopted.valid_index(i) then
			adopted.put(Void,i)
			adopted.put(adopted.last,i)
			adopted.remove_last
		 end
	end

	eif_access (a_pointer: POINTER): ANY is
		require
			a_pointer_not_void: a_pointer /= Default_pointer
		do
			Result := a_pointer.to_any
		ensure
			eif_access_not_void: Result /= Void
		end
#endif
#ifdef VE
	eif_adopt (a_object: ANY): POINTER is
		do
			ve_adopt (a_object)
			Result := wg_object_to_pointer (a_object)
		end

	wg_object_to_pointer (a_object: ANY): POINTER is
		external
			"C"
		alias
			"ewg_object_to_pointer"
		end
	
	ve_adopt (a_object: ANY) is
		require
			a_object_not_void: a_object /= Void
		external
			"C"
		alias
			"eif_adopt"
		end

	eif_wean (a_pointer: POINTER) is
		require
			a_pointer_not_void: a_pointer /= Default_pointer
		external
			"C"
		alias
			"eif_wean"
		end

	eif_access (a_pointer: POINTER): ANY is
		require
			a_pointer_not_void: a_pointer /= Default_pointer
		external
			"C"
		alias
			"eif_access"
		ensure
			eif_access_not_void: Result /= Void
		end
#endif
end
	
